# 光照参数
在main.cpp中声明光照的参数，然后将这些参数搞到shader中
```
//平行光：参数（方向、光强）uniform变量形式
glm::vec3 lightDirection = glm::vec3(-1.0f, -1.0f, -1.0f);
glm::vec3 lightColor = glm::vec3(0.9f, 0.85f, 0.75f);
```
到`fragment.glsl`中声明参数，并且先暂时将`lightColor`直接输出验证是否正确
```
...
//光源参数
uniform vec3 lightDirection;
uniform vec3 lightColor;

void main()
{
    //1 将vs中输入的normal做一下归一化
    vec3 normalN = normalize(normal);
    //2 将负数的情况直接清理为0，使用clamp函数
    vec3 normalColor = clamp(normalN, 0.0, 1.0);
    FragColor = vec4(lightColor, 1.0);

    //FragColor = texture(sampler, uv);
}
```
之前设计的传输vec3不好用，设计一个专门传vector3向量的
```
void Shader::setVector3(const std::string& name, const glm::vec3 value)
{
    //1 通过名称拿到Uniform变量位置location
    GLuint location = GL_CALL(glGetUniformLocation(mProgram, name.c_str()));

    //2 通过location更新Uniform变量的值
    GL_CALL(glUniform3f(location, value.x, value.y, value.z));
}
```
到`render()`中调用方法并且绑定光照参数到`shader`中
```
void render(){
	...
    //光源参数的uniform更新
    shader->setVector3("lightDirection", lightDirection);
    shader->setVector3("lightColor", lightColor);
    ...
}
```
就可以得到目标效果啦

![输入图片说明](/imgs/2024-11-22/eR7Dk8ZpNNqxIKQW.png)

# Diffuse
## 实现
现在我们要实现Diffuse光照效果了
只需要到`fragment.glsl`修改，
```
#version 330 core
out vec4 FragColor;

uniform float time;

in vec2 uv;
in vec3 normal;

uniform sampler2D sampler;

//光源参数
uniform vec3 lightDirection;
uniform vec3 lightColor;

void main()
{
    //1 获取物体当前像素的颜色，也就是纹理的颜色
    vec3 objectColor = texture(sampler, uv).xyz;

    //2 准备diffuse（漫反射）相关的各类数据
    vec3 normalN = normalize(normal);

    vec3 lightDirN = normalize(lightDirection);
	
	//此处的diffuse需要clamp是因为，背后的面可能被点乘出负数的结果
	//虽然都是黑色，但是不太好
    float diffuse = clamp(dot(-lightDirN, normalN), 0.0, 1.0);
    vec3 finalColor = lightColor * diffuse * objectColor;

    FragColor = vec4(finalColor, 1.0);
}
```
修改一下平行光的方向就可以很明显的看到明暗关系
```
glm::vec3 lightDirection = glm::vec3(-0.4f, -1.4f, -1.9f);
glm::vec3 lightColor = glm::vec3(1.0f, 1.0f, 1.0f);
```

![输入图片说明](/imgs/2024-11-22/FcZQKRwLcVtCjVJd.png)

背后是黑色的是因为diffuse的值计算出来为0，从法线和光源方向的点乘入手就可以很容易理解了

![输入图片说明](/imgs/2024-11-22/YI5OATbqiPieS6Ro.png)

# Specular
## 实现
`reflect`是一个用来计算`lightRef`的函数，只需要传入光源`lightDir`方向和法线`normal`就行
这里注意一下，计算`specular`的`lightRef`和`ViewDir`都要是单位向量，要不然这个`max`就会失效
此处`cos`值为负数的情况，自己模拟一下便知，比如`view`视角跑到左半边去

![输入图片说明](/imgs/2024-11-22/S1wqt76og7gdagBY.png)![输入图片说明](/imgs/2024-11-22/1lslhw6AwqVokbLj.png)

由于针对每个像素，相机位置都是相同的，所以用`uniform`

![输入图片说明](/imgs/2024-11-22/v96MtWenPYPvxa51.png)

![输入图片说明](/imgs/2024-11-22/gsKwqoibzsuy8jGb.png)

### 求视线方向
开始实现：
在`vertexShader`中我们声明一个`worldPosition`并传给`fragmentShader`，`worldPosition`就是`worldPosition = transformPosition.xyz`，
```
#version 460 core
layout (location = 0) in vec3 aPos;
layout (location = 1) in vec2 aUV;
layout (location = 2) in vec3 aNormal;

out vec2 uv;
out vec3 normal;
out vec3 worldPosition;

uniform mat4 modelMatrix;
uniform mat4 viewMatrix;
uniform mat4 projectionMatrix;

void main()
{
    //将输入的顶点位置，转化为齐次坐标（3维->4维）
    vec4 transformPosition = vec4(aPos, 1.0);
    
    //做一个中间变量transformPosition，用于计算四维位置与modelMatrix相乘的中间结果
    transformPosition = modelMatrix * transformPosition;

    //计算当前顶点的worldPosition，并向后传输给FragmentShader
    worldPosition = transformPosition.xyz;

    gl_Position = projectionMatrix * viewMatrix * transformPosition;

    uv = aUV;
    normal = aNormal;
}
```
用in接入`vertexShader`传过来的`worldPosition`，然后用`worldPosition - cameraPosition`得到`viewDir`，并且`viewDir`需要归一化，而且需要夹值`0~1`，输出`viewDir`进行测试
```
...
in vec2 uv;
in vec3 normal;
in vec3 worldPosition;

uniform sampler2D sampler;

//光源参数
uniform vec3 lightDirection;
uniform vec3 lightColor;

//相机世界位置
uniform vec3 cameraPosition;

void main()
{
	...
    //测试
    vec3 viewDir = normalize(worldPosition - cameraPosition);
    viewDir = clamp(-viewDir, 0.0, 1.0);

    FragColor = vec4(viewDir, 1.0);
}
```
到`main.cpp`中，进行`shader`传参
```
void render(){
	...
    shader->setVector3("cameraPosition", camera->mPosition);
	...
}
```
到目前为止，我们已经准备好了`lightDir`和`viewDir`
### 求反射方向lightRef以及计算finalColor
利用`reflect`函数求出反射方向，
然后我们分别计算`diffuse`和`specular`光照，加一起得到`finalColor`并输出即可
```
...
//光源参数
uniform vec3 lightDirection;
uniform vec3 lightColor;

//相机世界位置
uniform vec3 cameraPosition;

void main()
{
    //计算光照的通用数据
    vec3 objectColor = texture(sampler, uv).xyz;
    vec3 normalN = normalize(normal);
    vec3 lightDirN = normalize(lightDirection);
    vec3 viewDir = normalize(worldPosition - cameraPosition);

    //计算diffuse
    float diffuse = clamp(dot(-lightDirN, normalN), 0.0, 1.0);
    vec3 diffuseColor = lightColor * diffuse * objectColor;

    //计算specular
    vec3 lightReflect = normalize(reflect(lightDirN, normalN));
    float specular = clamp(dot(lightReflect, -viewDir), 0.0, 1.0);
    vec3 specularColor = lightColor * specular;

    vec3 finalColor = diffuseColor + specularColor;

    FragColor = vec4(finalColor, 1.0);
}
```
效果如下：

![输入图片说明](/imgs/2024-11-22/WrJzLhwsomXDKlf7.png)

## 背后高光问题

但是转到背后会发现，没有被光照亮的地方也有高光，

![输入图片说明](/imgs/2024-11-22/WZ21DNgSelD81hjJ.png)

![输入图片说明](/imgs/2024-11-22/641JejjBr01FXgjX.png)

上图中的`.`是点乘的意思，通过点乘算出`cos`值，
R就是通过reflect算出的反射方向

![输入图片说明](/imgs/2024-11-22/8iI7znqXnf6A0aIt.png)

![输入图片说明](/imgs/2024-11-22/QARWpw68iuEczxEb.png)

在`fragmentShader`中，引入`step`函数用来判断
`dotResult`用来计算逆向光线与法线的夹角`cos`值，如果小于零才说明不是背面
```
void main()
{
    //计算光照的通用数据
    vec3 objectColor = texture(sampler, uv).xyz;
    vec3 normalN = normalize(normal);
    vec3 lightDirN = normalize(lightDirection);
    vec3 viewDir = normalize(worldPosition - cameraPosition);

    //计算diffuse
    float diffuse = clamp(dot(-lightDirN, normalN), 0.0, 1.0);
    vec3 diffuseColor = lightColor * diffuse * objectColor;

    //计算specular
    //防止背面光效果
    float dotResult = dot(-lightDirN, normalN);
    float flag = step(0.0, dotResult);

    vec3 lightReflect = normalize(reflect(lightDirN, normalN));
    float specular = clamp(dot(lightReflect, -viewDir), 0.0, 1.0);
    vec3 specularColor = lightColor * specular * flag;

    vec3 finalColor = diffuseColor + specularColor;

    FragColor = vec4(finalColor, 1.0);
}
```
## 镜面光斑大小调整

![输入图片说明](/imgs/2024-11-22/9VaBDsOIeGb7HX1E.png)

用n次方可以将`cos`值变得陡峭或平缓，`cos`值越小我们的光斑范围也就会越小

![输入图片说明](/imgs/2024-11-22/BLoUK55WDwHebdiU.png)

```
void main()
{
    ...
    float specular = clamp(dot(lightReflect, -viewDir), 0.0, 1.0);
    //控制光斑大小
    specular = pow(specular, 64);

    ...
}
```
![输入图片说明](/imgs/2024-11-22/WZzLMMXon2elJqLw.png)

## 强度调整
在`fragmentShader`中，加入一个`uniform`变量`specularIntensity`，用来控制强度
```
uniform float specularIntensity;

void main()
{
	...
    vec3 specularColor = lightColor * specular * flag * specularIntensity;
    ...
}
```
回到main.cpp中，声明并传给`shader`即可
```
float specularIntensity = 0.5f;
void render(){
	...
    shader->setFloat("specularIntensity", specularIntensity);
	...
}
```

# ambient环境光
很简单只要将原本`objectColor`乘上一个`vec3`的`ambientColor`即可
```
uniform sampler2D sampler;
uniform vec3 ambientColor;

void main()
{
	...
	vec3 objectColor = texture(sampler, uv).xyz;
	
    //环境光计算
    vec3 ambientColor = objectColor * ambientColor;
    vec3 finalColor = diffuseColor + specularColor + ambientColor;
    FragColor = vec4(finalColor, 1.0);
}
```
在main.cpp中声明`ambientColor`具体数值并传到`shader`中即可
```
//ambient
glm::vec3 ambientColor = glm::vec3(0.1f, 0.1f, 0.1f);
void render(){
	...
    shader->setVector3("ambientColor", ambientColor);
    ...
}
```
有了环境光，背后（不会被光照到的地方）就不会死黑了

![输入图片说明](/imgs/2024-11-22/VhoLHKK1nVASzGoY.png)
<!--stackedit_data:
eyJoaXN0b3J5IjpbLTExOTc2MzMwMzYsLTExOTEzNDgzNDIsMT
QxNzIwNjM4LDQxODg3Mjk5NiwxMjMzODM0MDIsLTEwODU0MDYz
NDIsLTU5NTYyMTA1OSwtMTc2NzAxNTAwNywxMDExODI2NjY1LD
Y4NzA1NzI1NywyNzUxODI5NjgsLTE2ODQ1OTU1MTgsMTM0MTI3
ODEyOCwtMTUyOTA0NDUxN119
-->